// The Hexa Compiler
// Copyright (C) 2018  Oleg Petrenko
// Copyright (C) 2018  Bogdan Danylchenko
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU Lesser General Public License as published by
// the Free Software Foundation, version 3 of the License.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU Lesser General Public License for more details.
//
// You should have received a copy of the GNU Lesser General Public License
// along with this program.  If not, see <https://www.gnu.org/licenses/>.

module
	// See: `/docs/parser.md`
	class TestParser {
		static function test() {
			console.log("TestParser begin")

			// Basics and indentaion
			shouldAllEqual([
				''			 : '<!--null-->',
				'  '		 : '<!--null-->',
				'	'		 : '<!--null-->',
				'   	'	 : '<!--null-->',

				'\n'		 : '<!--null-->',
				'\n\n'		 : '<!--null-->',
				'\r\r\n\r\n\r\t' : '<!--null-->',

				'{}' : 'TBlock([])',
				'{{}}' : 'TBlock([TBlock([])])',
				'{ 0 0 0 }' : 'TBlock([TInt(0),TInt(0),TInt(0)])',

				'0' 		 : 'TInt(0)',
				' 0 '		 : 'TInt(0)',
				'123'		 : 'TInt(123)',
				'12'		 : 'TInt(12)',
				"0x1"		 : 'TInt(0x1)',
				"0x0"		 : 'TInt(0x0)',
				"0xF"		 : 'TInt(0xF)',
				"0xFA"		 : 'TInt(0xFA)',
				"0xFABCDEF" : 'TInt(0xFABCDEF)',

				"0.0"		 : 'TFloat(0.0)',
				"0.123" 	 : 'TFloat(0.123)',

				"'s'"		 : 'TString(s)',
				"\"s\"" 	 : 'TString(s)'
			])

			shouldAllEqual([
				'1 2 3 trace("Hello!", "World!") + 5 * 6 / 3':
					'TBlock([
					TInt(1),
					TInt(2),
					TInt(3),
					TBinop(
					TCall(TIdent(trace),[TString(Hello!),TString(World!)]),
					+,
					TBinop(TInt(5),*,TBinop(TInt(6),/,TInt(3))))])',

				'enum Test { Demo } hello World':
					'TBlock([TEnum(Type(Test),[NodeTypeValue(Type(Demo))]),
					TIdent(hello),
					NodeTypeValue(Type(World))])'
			])

			// Operators
			shouldAllEqual([
				'i ++ ++ i': "TBlock([TUnop(++,true,TIdent(i)),TUnop(++,false,TIdent(i))])",
				'a + b': "TBinop(TIdent(a),+,TIdent(b))",
				'a += b': "TAssignop(TIdent(a),+,TIdent(b))"
			])

			// String interpolation
			shouldAllEqual([
				'"\\\\(v)"': "TString(\\\\(v))",
				'"\\(v)"': 'TParenthesis(TBinop(TString(),+,TBinop(TParenthesis(TIdent(v)),+,TString())))',
				'"\\(((v)))"': 'TParenthesis(TBinop(TString(),+,TBinop(TParenthesis(TParenthesis(TParenthesis(TIdent(v)))),+,TString())))',
				'"\\( v )"': 'TParenthesis(TBinop(TString(),+,TBinop(TParenthesis(TIdent(v)),+,TString())))',
				'"\\(V)\\(v)"': 'TParenthesis(TBinop(TString(),+,TBinop(TParenthesis(NodeTypeValue(Type(V))),+,TBinop(TString(),+,TBinop(TParenthesis(TIdent(v)),+,TString())))))',
				'"\\(V)\\(v)s\\(v)"': 'TParenthesis(TBinop(TString(),+,TBinop(TParenthesis(NodeTypeValue(Type(V))),+,TBinop(TString(),+,TBinop(TParenthesis(TIdent(v)),+,TBinop(TString(s),+,TBinop(TParenthesis(TIdent(v)),+,TString())))))))',
				'"\\(V)s\\(v)s\\(v)"': 'TParenthesis(TBinop(TString(),+,TBinop(TParenthesis(NodeTypeValue(Type(V))),+,TBinop(TString(s),+,TBinop(TParenthesis(TIdent(v)),+,TBinop(TString(s),+,TBinop(TParenthesis(TIdent(v)),+,TString())))))))'
			])

			shouldAllEqualWithoutTrim([
				'"\\( v )"': 'TParenthesis(TBinop(TString(),+,TBinop(TParenthesis(TIdent(v)),+,TString())))',
				'"\\(V)\r\n\\(v)"': 'TParenthesis(TBinop(TString(),+,TBinop(TParenthesis(NodeTypeValue(Type(V))),+,TBinop(TString(\n),+,TBinop(TParenthesis(TIdent(v)),+,TString())))))',
				'"\\(V) \\(v) \\(v)"': 'TParenthesis(TBinop(TString(),+,TBinop(TParenthesis(NodeTypeValue(Type(V))),+,TBinop(TString( ),+,TBinop(TParenthesis(TIdent(v)),+,TBinop(TString( ),+,TBinop(TParenthesis(TIdent(v)),+,TString())))))))'
			])

			// Modularity
			shouldAllEqual([

				// New syntax
				'module { }': 'TBlock([TModule(,[]),TBlock([])])',
				'module': 'TModule(, [])',
				'code module x code': 'TBlock([TIdent(code),TModule(x,[TIdent(code)])])',
				'code module x module y code': 'TBlock([TIdent(code),TModule(x,[]),TModule(y,[TIdent(code)])])',
				'code module x module y code {code}': 'TBlock([TIdent(code),TModule(x,[]),TModule(y,[TIdent(code)]),TBlock([TIdent(code)])])',
				'module a': 'TModule(a, [])',
				'module a.b': 'TModule(a.b, [])',
				'module class Inner { }': 'TModule(,[TClass(Type(Inner),null,[],[],false)])',
				'module private class Inner {}' : 'TModule(,[TPrivate(TClass(Type(Inner),null,[],[],false))])',
				'module a module b': 'TBlock([TModule(a,[]),TModule(b,[])])'
			])

			shouldAllEqual([
				'import "test"': 'Import([],test)',
				'import xxx in "test"': 'Import([Lower(xxx)],test)',
				'import xxx as yyy in "test"': 'Import([Lower(xxx){as}Lower(yyy)],test)',
				'import XXX as YYY in "test"': 'Import([Upper(XXX){as}Upper(YYY)],test)',
				'import * as mmm in "test"': 'Import([*{as}Lower(mmm)],test)',
				'import XXX, YYY as ZZZ, www in "test"': 'Import([Upper(XXX),Upper(YYY){as}Upper(ZZZ),Lower(www)],test)'
			])

			shouldAllError([
				'import',
				//'import ""',
				'import hehe',
				'import _ as B in "test"',
				'import * in "test"',
				'import * as B in "test"'
			//	'import a as B in ""'
			])

			// Types parsing in expressions
			shouldAllEqual([
				'var a = new Array<A>()' : 'TVar(a,null,TNew([],ParametricType(Array,[Type(A)]),[],[],[],[]),false,false)',
				'a = Array<A>.staticField()' : 'TBinop(TIdent(a),=,TCall(TDot(NodeTypeValue(ParametricType(Array,[Type(A)])),staticField),[]))',
				'a = EnumTest.EnumField' : 'TBinop(TIdent(a),=,TDotUpper(NodeTypeValue(Type(EnumTest)),EnumField))',
				'a = EnumTest.EnumField(arg)' : 'TBinop(TIdent(a),=,TCall(TDotUpper(NodeTypeValue(Type(EnumTest)),EnumField),[TIdent(arg)]))',
				'a = EnumTest.EnumField(argName: argValue, arg2, arg3: arg3)' : 'TBinop(TIdent(a),=,TCall(TDotUpper(NodeTypeValue(Type(EnumTest)),EnumField),[argName:TIdent(argValue),TIdent(arg2),arg3:TIdent(arg3)]))',
				'a = EnumTest<A,B>.EnumField' : 'TBinop(TIdent(a),=,TDotUpper(NodeTypeValue(ParametricType(EnumTest,[Type(A),Type(B)])),EnumField))',
				'var a = b as B, c = d as! B, e = f as? B' : 'TVars([TVar(a,null,TAs(TIdent(b),<!--default-->,Type(B)),false,false),TVar(c,null,TAs(TIdent(d),!,Type(B)),false,false),TVar(e,null,TAs(TIdent(f),?,Type(B)),false,false)])',
				'var a = b is B, c = d is B, e = f is B' : 'TVars([TVar(a,null,TAs(TIdent(b),Type(B)),false,false),TVar(c,null,TAs(TIdent(d),Type(B)),false,false),TVar(e,null,TAs(TIdent(f),Type(B)),false,false)])'
			])

			// Types parsing in declarations
			shouldAllEqual([
				'var x:[Array<T>]' : 'TVar(x,ParametricType(Array,[ParametricType(Array,[Type(T)])]),null,false,false)',
				'var x:[Map<K,V> : Array<T>]' : 'TVar(x,ParametricType(Map,[ParametricType(Map,[Type(K),Type(V)]),ParametricType(Array,[Type(T)])]),null,false,false)',
				'var x:{}, y:[], z:[:], w:()=>{}' : 'TVars([TVar(x,Object([],[]),null,false,false),TVar(y,ParametricType(Array,[Object([],[])]),null,false,false),TVar(z,ParametricType(Map,[Object([],[]),Object([],[])]),null,false,false),TVar(w,Function([],Object([],[])),null,false,false)])',
				'let x:()=>()=>()=>()=>Void' : 'TVar(x,Function([],Function([],Function([],Function([],Type(Void))))),null,true,false)'
			])

			// Enumerations
			shouldAllEqual([
				'enum A {}' : 'TEnum(Type(A),[])',
				'enum A { A B C }' : 'TEnum(Type(A),[NodeTypeValue(Type(A)),NodeTypeValue(Type(B)),NodeTypeValue(Type(C))])',
				'enum A { A(v:Int) B C(v:[K:V], a:Array<T>) }' :
					'TEnum(Type(A),[
					TCall(NodeTypeValue(Type(A)),[v:NodeTypeValue(Type(Int))]),
					NodeTypeValue(Type(B)),
					TCall(NodeTypeValue(Type(C)),[v:TMap([NodeTypeValue(Type(K))],[NodeTypeValue(Type(V))]),a:NodeTypeValue(ParametricType(Array,[Type(T)]))])
					])',
				'enum A { E(v: T<T>?) }' : 'TEnum(Type(A),[TCall(NodeTypeValue(Type(E)),[v:NodeTypeValue(Optional(ParametricType(T,[Type(T)])))])])',
				'enum A { E(v: T<T?>) }' : 'TEnum(Type(A),[TCall(NodeTypeValue(Type(E)),[v:NodeTypeValue(ParametricType(T,[Optional(Type(T))]))])])',
				'enum A { E(v: T?) }' : 'TEnum(Type(A),[TCall(NodeTypeValue(Type(E)),[v:NodeTypeValue(Optional(Type(T)))])])'
			])

			// Constructor
			shouldAllEqual([
				'new A(a: 1, b: 2)' : 'TNew([],Type(A),[TInt(1),TInt(2)],[],[],[a,b])',
				'class A {} var a = new A { } ()' : "TBlock([TClass(Type(A),null,[],[],false),TVar(a,null,TNew([],Type(A),[],[],[],[]),false,false)])",
				'class A { var field: String } var a = new A { field: "Value" } ()' :
					"TBlock([TClass(Type(A),null,[],[TVar(field,Type(String),null,false,false)],false),TVar(a,null,TNew([],Type(A),[],[field],[TString(Value)],[]),false,false)])",
				'class A { var field: String var otherfield: Int } var a = new A { field: "Value", otherfield: 25 } ()' :
					"TBlock([TClass(Type(A),null,[],[TVar(field,Type(String),null,false,false),TVar(otherfield,Type(Int),null,false,false)],false),TVar(a,null,TNew([],Type(A),[],[field,otherfield],[TString(Value),TInt(25)],[]),false,false)])"
			])

			// Functions
			shouldAllError([
				'class A { new(a b) {} }',
				'class A { new(a, b,) {} }',
				'class A { new(,) {} }',
			 	'fun f(a b c) {}',
			 	'fun f(a b) {}',
			 	'fun f(a, b,) {}',
			 	'fun f(a,) {}',
			 	'fun f(,) {}'
			])

			// Property
			shouldAllError([
				'class A { var i: Int { set (value) {} } }', // set before get
				'class A { var i: Int { get {} set () {} } }', // No `value` in setter
				'class A { var i: Int { get {} set {} } }', // No `(value)` in setter
				'class A { let i: Int { get {} set (value) {} } }', // set in let
				'class A { let i: Int { get {} set (value, val) {} } }', // Multiple `value`
				'class A { var i: Int { get {} set (Value) {} } }', // Upper `Value`
				'class A { var i: Int { get (value) {} set (value) {} } }', // `value` in get
				'class A { var i: Int {} }' // No get/set
				// TODO private set/private get
			])

			// Commas
			shouldAllError([
				'if(,) {}',
				'if(a,) {}',
				'switch (,) {}',
				'switch (a,) {}',
				'@att(,) x',
				'@att(a,) x',
				'@att(a,b,) x',
				'@att(a b,) x',
				'@att(a b) x',
				'new T(,)',
				'new T(a b,)',
				'new T(a,b,)',
				'new T(a b)',
				'call(1 2)',
				'call(1 2,)',
				'call(1,2,)',
				'call(1,)',
				'call(a:1,)',
				'call(1:1,)',
				'call(a:,)',
				'call(a:)',
				'call(,)',
			 	'[1 2]',
			 	'[,]',
			 	'[1,]',
			 	'[1,2,]',
			 	'[1:1 2:2]',
			 	'[1:1,2:2,]',
			 	'{a:1 b:2}',
			 	'{,}',
			 	'{a:1,}',
			 	'{a:1,b:2,}'
			])

			// Misc
			shouldAllError([
				'=',
				'= 1'
			])

			// Conditions
			shouldAllEqual([
				'if (this) { this }' : 'TIf([This],TBlock([This]),<!--null-->)',
				'if (this, this) { this } else { break }' : 'TIf([This,This],TBlock([This]),TBlock([Break]))',
				'if (this, let a = this) { this } else { break }' : 'TIf([This,TVar(a,null,This,true,false)],TBlock([This]),TBlock([Break]))',
				'if (let a = this, let b = this, this) { this }' : 'TIf([TVar(a,null,This,true,false),TVar(b,null,This,true,false),This],TBlock([This]),<!--null-->)'
			])

			// External declarations
			shouldAllEqual([
				'declare var a: T' : 'TVar(a,Type(T),null,false,true)',
				'declare let a: T' : 'TVar(a,Type(T),null,true,true)',
				'declare fun name()' : 'TFunction(name,null,[],null,true)',
				'declare fun name() hi()' : 'TBlock([TFunction(name,null,[],null,true),TCall(TIdent(hi),[])])',
				// External functions do not parse body
				'declare fun name() {}' : 'TBlock([TFunction(name,null,[],null,true),TBlock([])])',
				'declare class A {}' : 'TClass(Type(A),null,[],[],true)',
				'declare A = B' : 'TypeAlias(Type(A),Type(B))'
			])

			shouldAllError([
				"declare var a, b, c",
				"declare anything"
			])

			// Array and map
			shouldAllEqual([
				'push([:])' : 'TCall(TIdent(push),[TMap([],[])])',
				'push([])' : 'TCall(TIdent(push),[TArray([])])',
				'[].length' : 'TDot(TArray([]),length)',
				'[:].length' : 'TDot(TMap([],[]),length)',
				'[[:]]': 'TArray([TMap([],[])])'
			])

			console.log("TestParser done \(Math.round((passed/overall)*100))% (\(passed)/\(overall))")
		}

		static var passed = 0
		static var overall = 0

		//
		// Helpers
		//

		/// Parses `input` into AST and stringifies, checks by trimmed `test`
		static function shouldEqual(input: String, test: String) {
			let test = TestParser.deepTrim(test)
			shouldEqualWithoutTrim(input, test)
		}

		/// Same as `shouldEqual`, but does no trimming of `test`
		static function shouldEqualWithoutTrim(input: String, test: String) {
			overall++
			var lexe = Lexer.tokenize(Buffer.fromString(input), "TEST")
			var parsed = Parser.toNode(Parser.parseNodes(lexe))
			var res = stringify(parsed)
			if (test != res) {
				throw 'TestParser test fail: `\(input)`\n!==: `\(test)`\nGot: `\(res)`\nParsed: `\(parsed)`'
			}
			passed++
		}

		/// Operates `shouldEqual` on all key-value pairs of `input => test`
		static function shouldAllEqual(map: [String : String]) {
			for (input in map.keys()) {
				var test = map.get(input)
				shouldEqual(input, test)
			}
		}

		/// Does `shouldEqual` without trimming `=> test` parts
		static function shouldAllEqualWithoutTrim(map: [String : String]) {
			for (input in map.keys()) {
				var test = map.get(input)
				shouldEqualWithoutTrim(input, test)
			}
		}

		static function shouldError(input: String) {
			overall++
			try {
				var lexe = Lexer.tokenize(Buffer.fromString(input), "TEST")
				var parser = Parser.toNode(Parser.parseNodes(lexe))
			}
			catch(e: Any) {
				passed++
				return
			}
			throw 'TestParser test fail: `\(input)` did not throw exception.'
		}

		static function shouldAllError(input: [String]) {
			for (str in input) {
				shouldError(str)
			}
		}

		/// Removes all internal and trailing whitespace and newlines
		static function deepTrim(s: String) {
			return s.split('\n').join('').split('\r').join('').split('\t').join('').split(' ').join('')
		}

		/// Creates a testable consistent string representation of node
		static function stringify(node: Node): String {
			switch (node) {
			case null: return '<!--null-->'

			// Have no sub-nodes
			case Node.TString(s): return 'TString(\(s))'
			case Node.TIdent(s): return 'TIdent(\(s))'
			case Node.TBool(b): return 'TBool(\(b))'
			case Node.This: return 'This'
			case Node.Super: return 'Super'
			case Node.TInt(s): return 'TInt(\(s))'
			case Node.TFloat(s): return 'TFloat(\(s))'
			case Node.Null: return 'Null'
			case Node.Break: return 'Break'
			case Node.Continue: return 'Continue'

			// Have sub-nodes
			case Node.TIs(expr, type): return 'TAs(' + TestParser.stringify(expr) + "," + TestParser.stringifyType(type) + ")"
			case Node.TAs(expr, kind, type): return 'TAs(' + TestParser.stringify(expr) + "," + Token.stringify(kind) + "," + TestParser.stringifyType(type) + ")"
			case Node.TBinop(a, op, b): return 'TBinop(' + TestParser.stringify(a) + ',' + Token.stringify(op) + ',' + TestParser.stringify(b) + ')'
			case Node.TAssignop(a, op, b): return 'TAssignop(' + TestParser.stringify(a) + ',' + Token.stringify(op) + ',' + TestParser.stringify(b) + ')'
			case Node.TBlock(els): return 'TBlock(' + TestParser.stringifyNodeArray(els) + ')'
			case Node.TVar(name, t, expr, const, external):
			return 'TVar(\(name),' + ((t != null) ? TestParser.stringifyType(t) : "null") +
			"," + ((expr != null) ? TestParser.stringify(expr) : "null") +',\(const),\(external))'
			case Node.TVars(vars): return 'TVars(' + TestParser.stringifyNodeArray(vars) + ")"

			case Node.TFunction(name, expr, vars, rettype, external):
				return 'TFunction(\(name),'
				+ (expr == null ? "null" : TestParser.stringify(expr))
				+ "," + TestParser.stringifyNodeArray(vars) + ',' + ((rettype != null) ? TestParser.stringifyType(rettype) : "null") + ",\(external))"

			case Node.TArrow(expr, vars, rettype):
				return 'TArrow('
				+ (expr == null ? "null" : TestParser.stringify(expr))
				+ "," + TestParser.stringifyNodeArray(vars) + ',' + ((rettype != null) ? TestParser.stringifyType(rettype) : "null") + ")"

			case Node.TCall(e, el, argNames):
				var res = 'TCall(' + TestParser.stringify(e) + ',['
				for (i in el.length) {
					res += (argNames[i] == null) ? "" : argNames[i] + ":"
					res += TestParser.stringify(el[i]) + ((i != el.length - 1) ? "," : "")
				}
				return res + "])"

			case Node.TParenthesis(e): return 'TParenthesis(' + TestParser.stringify(e) + ')'
			case Node.TReturn(e): return 'TReturn(' + TestParser.stringify(e) + ')'
			case Node.TThrow(e): return 'TThrow(' + TestParser.stringify(e) + ')'
			case Node.TEnum(t, els): return 'TEnum(' + TestParser.stringifyType(t) + ',' + TestParser.stringifyNodeArray(els) + ')'
			case Node.TModule(paths, els):
				return "TModule(" + paths.join('.') + "," + TestParser.stringifyNodeArray(els) + ")"
			case Node.Import(els, path):
				function stringifyImportNode(node: ImportNode): String {
					switch (node) {
						case ImportNode.Lower(name): return 'Lower(\(name))'
						case ImportNode.Upper(name): return 'Upper(\(name))'
						case ImportNode.AllTheThings(_): return '*'
						case ImportNode.As(left, right):  return stringifyImportNode(left) + '{as}' + stringifyImportNode(right)
					}
				}

				return "Import([" + [for(p in els) stringifyImportNode(p)].join(',') + "]," + path + ")"

			case Node.TMap(k, v): return 'TMap([' + [for(p in k) TestParser.stringify(p)].join(',') + '],[' + [for(p in v) TestParser.stringify(p)].join(',') + '])'

			case Node.TClass(type, extend, implement, fields, external):
				var res = "TClass(" + TestParser.stringifyType(type) + ","
				res += (extend != null) ? TestParser.stringifyType(extend) : "null" + ","
				res += TestParser.stringifyNodeTypeArray(implement) + ","
				res += TestParser.stringifyNodeArray(fields) + ","
				res += external + ")"
				return res

			case Node.TNew(path, t, args, names, values, argNames):
				return 'TNew([' + path.join('.') + '],' + TestParser.stringifyType(t) + ',' + TestParser.stringifyNodeArray(args) + ',[' + names.join(",") + "]," + TestParser.stringifyNodeArray(values) + ",[" + argNames.join(',') + "])"

			case Node.NodeTypeValue(type): return 'NodeTypeValue(' + TestParser.stringifyType(type) + ")"
			case Node.TypeAlias(alias, value): return 'TypeAlias(' + TestParser.stringifyType(alias) + "," + TestParser.stringifyType(value) + ")"
			case Node.TDot(l, r): return "TDot(" + TestParser.stringify(l) + "," + r + ")"
			case Node.TDotUpper(l, r): return "TDotUpper(" + TestParser.stringify(l) + "," + r + ")"

			case Node.TPrivate(expr): return 'TPrivate(' + TestParser.stringify(expr) + ')'
			case Node.TIf(econd, eif, eelse):
				return 'TIf(' + TestParser.stringifyNodeArray(econd) + ',' + TestParser.stringify(eif) + ',' + TestParser.stringify(eelse) + ')'
			case Node.TArray(el): return 'TArray(' + TestParser.stringifyNodeArray(el) + ')'
			case Node.TUnop(op, postfix, e): return 'TUnop(' + Token.stringify(op) + ',' + postfix + ',' + TestParser.stringify(e) + ')'
			case _: throw '<!--' + node + '-->'
			}
		}

		static function stringifyNodeArray(arr: [Node]) {
			return "[" + [for (e in arr) TestParser.stringify(e)].join(',') + "]"
		}

		static function stringifyNodeTypeArray(arr: [NodeType]) {
			return "[" + [for (e in arr) TestParser.stringifyType(e)].join(',') + "]"
		}

		// Creates a testable consistent string representation of type
		static function stringifyType(node: NodeType): String {
			switch (node) {
			case NodeType.Type(s): return 'Type(\(s))'
			case NodeType.ParametricType(name, params): return 'ParametricType(\(name),[' + [for(p in params) TestParser.stringifyType(p)].join(',') + "])"
			case NodeType.Function(args, rettype): return 'Function([' + [for (e in args) TestParser.stringifyType(e)].join(',') + "]," + TestParser.stringifyType(rettype) + ')'
			case NodeType.Object(names, types): return 'Object([' + names.join(',') + "],[" + [for (e in types) TestParser.stringifyType(e)].join(',') +"])"
			case NodeType.Optional(type): return 'Optional(' + TestParser.stringifyType(type) + ')'
			case _: throw '<!--' + node + '-->'
			}
		}
	}
