// The Hexa Compiler
// Copyright (C) 2018-2022 Oleh Petrenko
// Copyright (C) 2018 Bogdan Danylchenko
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU Lesser General Public License as published by
// the Free Software Foundation, version 3 of the License.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU Lesser General Public License for more details.
//
// You should have received a copy of the GNU Lesser General Public License
// along with this program.  If not, see <https://www.gnu.org/licenses/>.

// Compiler version
let version = selfVersion

// Contents of hexa.json
// TODO all fields nullable cause it's .json
declare class Package {
	let name: String
	let version: String
	let description: String
	let author: String
	let license: String
	var addLicenseNoticeToOutput: Bool
	let randomSeed: Int?
	let define: Any
	let noLib: Bool
	// TODO allow libs as @roots
	let roots: [String]
	var global: [String]
	var entry: String
	let root: String
	// TODO error on duplicates
	var files: [String]
	let targets: [{
		name: String,
		generator: String,
		options: [String],
		/// Additional files for this target
		// TODO remove duplicates if multiple targets as the same time?
		files: [String],
		output: String
		// TODO defines per target to override global ones
	}]
}

class Main {
	var lines = 0
	var targetName = 'default'
	var targetIndex = 0
	var outputFolder: String? = null

	// Parses a file
	fun processFile(target: String, project: Project): [Node] {
		let content = Fs.readFileSync(target)
		let tokens = Lexer.tokenize(content, target)
		if (tokens.length > 0) {
			lines += tokens.line[tokens.length - 1]
		}
		let parsed = Parser.parseNodes(tokens, project)
		return parsed
	}

	fun simplyCompileToJs(buffer: Buffer, path: String): String? {
		process.argv[0] = __dirname + '/repl.json'
		let resolved = Path.resolve(__dirname + '/repl.hexa')
		fileContents.set(resolved, buffer)
		return new Main().perform(true)
	}

	// Read-eval-print loop, starts if hexa called without arguments
	fun repl() {
		console.log('\x1b[35m' + 'Hexa \(version) on node.js ' + Process.versions.node + '\x1b[0m')
		console.log('\x1b[33m' + 'To print something onto screen use `console.log(value)`' + '\x1b[0m')
		console.log('\x1b[33m' + 'To clear REPL memory use `.clear`' + '\x1b[0m')
		console.log('\x1b[33m' + 'To close REPL use `.exit`' + '\x1b[0m')
		// REPL
		var linesAll: [String] = []
		var linesLatest: [String] = []
		while true {
			var line = question('\x1b[32m' + "> " + '\x1b[0m').trim()
			if line == '.clear' {
				linesAll = []
				linesLatest = []
				continue
			}
			if line == '.exit' {
				return
			}
			if line.length == 0 {
				continue
			}
			linesLatest.push(line)
			try {
				var code = '"use strict"\r\n'
				try {
					let content = linesAll.join('\n') + '\n' + linesLatest.join('\n')
					let compiled = simplyCompileToJs(Buffer.from(content), "REPL")
					if compiled == null {
						linesLatest.pop()
						continue
					}
					if let compiled = compiled {
						code = compiled
					}
				} catch e: Any {
					let error = '' + e
					if error.indexOf('<!--end-->')  == -1 {
						// Do not remember previous command
						linesLatest = []
						console.error('\x1b[31mCompilation Error: ' + e, '\x1b[0m')
					} else {
						Process.stdout.write('\x1b[32m' + '>' + '\x1b[0m')
					}
					continue
				}

				// Success
				for line in linesLatest {
					linesAll.push(line)
				}
				linesLatest = []

				let result = eval(code)
			} catch e: Any {
				console.error("Error: " + e)
			}
		}
	}

	static fun processGlobals(project: Project, path: String, fullPath: String) {
		let index = project.moduleName.indexOf(fullPath ?? path)

		if let info = project.moduleInfo[index] {
			let nodes = info.nodes
			for e in nodes {
				var named = true
				switch e {
					case Var(_):
					case Enum(_):
					case Function(_):
					case Class(_):
					// TODO type alias
					case _:
						named = false
				}

				if named {
					let name = DataHelper.nameOf(e)
					if (project.globals.has(name)) {
						let data = project.data.get(e)
						let already = project.data.get(project.globals.get(name))
						let msg = "Global node `\(name)` declared in `\(path)` already defined at `\(already.fileName)` line `\(already.line)`"
						// TODO format properly and fit into lang server
						throw new CompilerErrors([new CompilerError(Fail.ProjectError, msg, data.line, data.column, data.fileName)])
					} else {
						project.globals.set(name, e)
					}
				}
			}
		}
	}

	/// Returns counted lines of code
	static fun processModule(project: Project, path: String, global: Bool, relative: Module? = null, node: Node? = null): Int {
		var lines = 0


		fun tryInRoot(root: String, fullPath: String) {
			let file = Path.join(root, path + '.hexa')
			if (Fs.existsSync(file)) {
				try {
				let resolved = Path.resolve(file)
				var cachedBuffer = Buffer.from('')

				if let cached = fileContents.get(resolved) {
					cachedBuffer = cached
				} else {
					cachedBuffer = Fs.readFileSync(file)
				}

				let content = cachedBuffer
				let tokens = Lexer.tokenize(content, file)
				if (tokens.length > 0) {
					lines += tokens.line[tokens.length - 1]
					// TODO --lines console.log(file, tokens.line[tokens.length - 1], 'lines')
				}

				let parsed = Parser.parseNodes(tokens, project)
				project.moduleName.push(fullPath)
				let info = new Module()
				info.nodes = parsed
				info.file = file
				info.path = fullPath
				project.moduleInfo.push(info)
				project.mapModules.set(node, info)

				for e in parsed {
					switch e {
						case Import(_, path):
							// TODO lines
							processModule(project, path, false, info, e)
					}
				}

				if (global == true) {
					processGlobals(project, path, fullPath)
				}

				return lines
			} catch e: Any {
				throw e
			}
			}

			return -1
		}

		var roots = project.roots

		if let relative = relative, not path.startsWith('/') {
			let root = Path.dirname(relative.file)
			roots = [root]
		}

		for root in roots {
			// Find project-relative path
			let file = Path.join(root, path + '.hexa')
			var fullPath = ''
			if Fs.existsSync(file) {
				fullPath = Path.relative(Path.dirname(project.json), file).slice(0,-5).split('\\').join('/')
			}

			// Do not parse again, but upload all globals
			let index = project.moduleName.indexOf(fullPath)
			if index > -1 {
				if global == true {
					processGlobals(project, path, fullPath)
				}

				project.mapModules.set(node, project.moduleInfo[index])
				return lines
			}

			// Process
			let lines = tryInRoot(root, fullPath)
			if lines > -1 {
				return lines
			}
		}

		var msg = "Cannot find file `\(path)` in any of the roots.\n"
		if path.endsWith('.hexa') {
			msg += "Do not write `.hexa` extension in imports, just use `import ... in '\(path.replace('.hexa', ''))'`.\n"
		}

		for root in roots {
			let file = Path.join(root, path + '.hexa')
			msg += "Checked here: `\(file)`\n"
		}

		if let data = project.data.get(node) {
			throw new CompilerErrors([new CompilerError(Fail.TyperError, msg, data.line, data.column, data.fileName)])
		}

		throw new CompilerErrors([new CompilerError(Fail.ProjectError, msg, 1, 1, project.json)])
	}

	// Parses all files, shows errors and stops if any
	fun processFiles(targets: [String], relativePath: String, loaded: [String: Bool], project: Project): [[Node]] {
		var res: [[Node]] = []
		let imports: [String] = []
		for target in targets {
			if not Fs.existsSync(target) {
				console.log('No such source file on disk `\(target)`')
				continue
			}

			try {
				let parsed = processFile(target, project)
				res.push(parsed)

				for e in parsed { switch e {
					case Import(el, path):
						let resolved = Path.resolve(relativePath + path + '.hexa')
						console.log('processFiles! ' + resolved)

						if loaded.get(resolved) == true {
							console.log('loaded! ' + resolved)
						}

						if loaded.get(resolved) != true {
							loaded.set(resolved, true)
							imports.push(resolved)
						}
				}}
			} catch e: Any {
				console.log(quote('\x1b[31m' + e.toString() + '\x1b[0m'))
			}
		}
		if targets.length != res.length {
			console.log('[Stopped on errors]')
			Process.exit(1)
		}
		if imports.length > 0 {
			// TODO [...a, ...b]
			for file in processFiles(imports, relativePath, loaded, project) {
				res.unshift(file)
			}
		}
		return res
	}

	fun incorrect(what: String, project: Project) {
		let msg = "Incorrect package file! Field `\(what)` does not exists"
		throw new CompilerErrors([new CompilerError(Fail.ProjectError, msg, 1, 1, project.json)])
	}

	fun readPackage(filename: String): Package {
		try {
			let pack: Package = JSON.parse(Fs.readFileSync(Path.resolve(filename)).toString('utf8').split('\r\n').join('\n'))
			return pack
		} catch e: Any {
			let msg = 'Cannot open ' + filename
			throw new CompilerErrors([new CompilerError(Fail.ProjectError, msg, 1, 1, 'hexa.json')])
		}
	}

	// Loads hexa.json
	fun loadPackage(filename: String, target: String, project: Project): Package {
		let pack: Package = readPackage(filename)
		project.json = filename

		if let name = pack.name {
			project.name = name
		} else {
			incorrect('name', project)
		}

		if pack.targets == null {
			incorrect('targets', project)
		}

		if let entry = pack.entry {
			if not entry.startsWith('/') {
				pack.entry = '/' + entry
			}
		} else {
			incorrect('entry', project)
		}

		if let define = pack.define { for key in Reflect.ownKeys(define) {
			if (!Reflect.has(project.defines, key)) { // `--define` has higher priority
				Reflect.set(project.defines, key, Reflect.get(define, key))
			}
		}}

		let t = (fun () {
			for t in pack.targets.length {
				if pack.targets[t].name == target {
					targetIndex = t
					return pack.targets[t]
				}
			}
			let msg = "Cannot find `\(target)` target in package file\n"
			throw new CompilerErrors([new CompilerError(Fail.ProjectError, msg, 1, 1, project.json)])
		})()

		let generator = availableGenerators(t.generator)
		if generator == null {
			let msg = "Generator `\(t.generator)` not available or not supported by this compiler version\n"
			throw new CompilerErrors([new CompilerError(Fail.ProjectError, msg, 1, 1, project.json)])
		}

		// Fill roots
		project.roots.push(Path.resolve(__dirname + '/library/'))
		let dir = Path.dirname(filename)

		if let roots = pack.roots {
			for root in roots {
				project.roots.push(Path.resolve(dir + '/' + root))
			}
		} else {
			project.roots.push(Path.resolve(dir))
		}

		// Add standard library
		if pack.files == null {
			pack.files = []
		}

		pack.global = pack.global ?? []
		for i in pack.global.length {
			if let file = pack.global[i], !file.startsWith('/') {
				pack.global[i] = '/' + file
			}
		}

		// for v : i in pack.global // TODO
		let global = pack.global
		global.forEach(fun (item, index) {
			if global.indexOf(item) != index {
				let msg = "Package `\(item)` included multiple times in the `global` section of `hexa.json`\n"
				throw new CompilerErrors([new CompilerError(Fail.ProjectError, msg, 1, 1, project.json)])
			}
		})

		if pack.noLib == true {
			// Nothing
		} else {
			let libraries: [String] = setLibrary(generator, t.options, project.defines)
			// Add files from specific build target
			for file in libraries {
				pack.global.unshift(file)
			}
		}

		// Defaults
		if pack.addLicenseNoticeToOutput == null {
			pack.addLicenseNoticeToOutput = true
		}

		return pack
	}

	/// Entry
	new () {}

	fun loadModules(project: Project, input: Package) {
		let moduleErrors: [CompilerError] = []

		if let global = input.global {
			for path in global {
				try {
					lines += processModule(project, path, true)
				} catch e: CompilerErrors {
					let errors: [CompilerError] = e.errors
					for error in errors {
						moduleErrors.push(error)
					}
				}
			}
		}

		try {
			processModule(project, input.entry, false)
		} catch e: CompilerErrors {
			let errors: [CompilerError] = e.errors
			for error in errors {
				moduleErrors.push(error)
			}
		}

		if moduleErrors.length > 0 {
			throw new CompilerErrors(moduleErrors)
		}
	}

	/// Parse arguments in any order
	fun parseArguments() {

	}

	/// Default actions
	fun perform(noWrite: Bool = false): String? {
		// Debug information

		/// Used to measure compiler performance and to show a success message
		let begin = Date.now()

		// Initialize compiler
		Lexer.init()

		// Set error format
		if Process.argv[0] == "--alt-error-format" {
			alternativeErrorFormat = true
			Process.argv.shift()
		}


		// Support `--output-folder path`
		if Process.argv[0] == "--output-folder" {
			Process.argv.shift()
			outputFolder = Process.argv.shift()
		}

		// Gather defines `--define name=value`
		let defines: Any = {}
		while Process.argv[0] == "--define" {
			if (Process.argv.length < 2) {
				console.log("Usage: `--define name=value`")
				Process.exit(1)
			}
			Process.argv.shift()
			let kv = (Process.argv.shift() ?? '').split('=')
			if kv.length < 2 {
				console.log("Usage: `--define name=value`")
				Process.exit(1)
			}
			let k = kv[0]
			let v = JSON.parse(kv[1])
			defines[k] = v
		}

		// Spawn project
		let project = new Project()
		project.defines = defines

		// Exit before self-test
		if Process.argv[0] == "syntax-linter" {
			if Process.argv.length < 2 {
				console.log("Usage: `hexa syntax-linter file.hexa`")
			} else {
				let file = Process.argv[1]
				try {
					let content = Fs.readFileSync(file)
					try {
						let tokens = Lexer.tokenize(content, file)
						try {
							let parser = Parser.parseNodes(tokens, project, true)
						} catch e: Any {
							console.log("\(e)")
						}
					} catch e: Any {
						console.log("\(e)")
					}
				} catch e: Any {
					console.log("ERROR: Unable to open file \(file)")
				}
			}
			Process.exit(0)
		}

		// Self-test
		#if debug
			#if times
				let beginTests = Date.now()
			#end
			// Integrated compiler tests, they compiled *into* `hexa` executable and run before compilation
			// Managed by `define.debug` of `hexa.json`
			TestLexer.test()
			TestParser.test()
			TestTyper.test()
			#if times
				console.log("[Tests finished in \(Date.now() - beginTests) ms]")
			#end
		#end

		fun ignoreArguments(fromIndex: Int)	{
			if fromIndex == Process.argv.length {
				return
			}
			let args = []
			let count: Int = Process.argv.length - fromIndex
			var i = 0
			while i < count {
				args.push(Process.argv[i++ + fromIndex])
			}
			console.log('Argument' + (args.length > 1? "s" : "") + ' `' + args.join("`, `") + '` ignored')
		}

		if Process.argv[0] == null {
			repl()
			Process.exit(0)
		}
		else if Process.argv[0] == "init" {
			init()
			ignoreArguments(3)
			Process.exit(0)
		}
		else if Process.argv[0] == "symlink" {
			symlink()
			Process.exit(0)
		}

		// Get inputs
		var currentFile = Path.resolve(Process.argv[0])
		var currentParsedFile: ParsedPath = Path.parse(currentFile)
		var input: Package = null as! Package
		var packageFolder: String = currentParsedFile.dir

		// Build the selected project
		if currentParsedFile.ext == ".json" {
			process.argv.shift()
			if process.argv[0] == 'targets' {
				let pack = readPackage(currentFile)
				if pack.name == null {
					incorrect('name', project)
				}
				if pack.targets == null {
					incorrect('targets', project)
				}

				let targets = []

				for target in pack.targets {
					targets.push(target.name)
				}

				console.log(quote('The project `\(pack.name)` has following targets: ' + targets.join(', ')))
				return null
			}
			if process.argv[0] == '--target' {
				process.argv.shift()
				targetName = process.argv[0] ?? 'default'
				process.argv.shift()
			}
			input = loadPackage(currentFile, targetName, project)
			ignoreArguments(0)

			if not noWrite {
				if targetName != 'default' {
					console.log("[Building \(input.name) - \(targetName)]")
				} else {
					console.log("[Building \(input.name)]")
				}

				console.log("[Using 1 of \(require('os').cpus().length) CPU cores]")
				// TODO ^ fix syntax highlighting for \parenthesis pairs
			}
		}
		// Search for project in the selected folder
		else if Fs.existsSync(currentFile), Fs.lstatSync(currentFile).isDirectory() {
			ignoreArguments(3)
			input = loadPackage(currentFile + Path.sep + "hexa.json", targetName, project)
			packageFolder = currentFile
		}
		// Evaluate file directly
		else if (currentParsedFile.ext == ".hexa") {
			//let code = '"use strict"\r\n' + GenJs.stringifyProject(Normalizer.normalize(Node.Block([processFile(currentFile)]), GenJs.reserved))
			try {
				eval(simplyCompileToJs(Fs.readFileSync(currentFile), currentFile))
			} catch e: Any {
				console.error("" + e)
			}
			Process.exit(0)
			return null
		} else {
			console.error('Unknown command: \(Process.argv[0])')
			console.error(quote('Try `hexa help`'))
			printVersion()
			Process.exit(1)
			return null
		}

		// Perform compilation
		#if times
			let beginParsing = Date.now()
		#end

		var files: [[Node]] = []
		if false, input.files != null {
			let relativePath = packageFolder + Path.sep
			let loaded: [String: Bool] = [:]
			let fileList = input.files.map(fun(file) {
				// Avoid global paths
				if (file.startsWith('/') || file.charAt(1) == ':') {
					return Path.resolve(file)
				}
				let resolved = Path.resolve(relativePath + file)
				loaded.set(resolved, true)
				return resolved
			})
			{
				files = processFiles(fileList, relativePath + '/' + input.root + '/', loaded, project)
			}
		}

		try {
			loadModules(project, input)
		} catch e: Any {
			console.error(e.toString())
			Process.exit(500)
		}

		#if times
			console.log("[Parsing finished in \(Date.now() - beginParsing) ms]")
			let beginTyper = Date.now()
		#end

		let typer = new Typer(project)
		try {
			typer.fillModules()
		} catch e: Any {
			console.log(quote('\x1b[31m' + e.toString() + '\x1b[0m'))
			if noWrite {
				return null
			}
			Process.exit(1)
		}

		#if times
			console.log("[Typer finished in \(Date.now() - beginTyper) ms]")
		#end

		let normalizer = new Normalizer(project)

		// TODO `let generators = [:]` should error
		// TODO not `Any`
		// TODO why not just `['js':spawn]`
		let generators: [String : () => Any] = [:]

		fun registerTarget(name: String, generator) {
			if let generator = generators.get(name) {
				console.log('Note: generator `\(name)` was overridden')
			}
			generators.set(name, generator)
		}

		registerTarget('js', GenJs.spawn)
		registerTarget('c', GenCxx.spawn)

		// Perform code generation
		if let generatorName = input.targets[targetIndex].generator {
			if let generatorSpawner = generators.get(generatorName) {
				let generator = generatorSpawner()

				// TODO outdated
				let target = packageFolder + Path.sep + Path.parse(input.targets[targetIndex].output).name + generator.extension

				var target = Path.join(packageFolder, input.targets[targetIndex].output)
				if let outputFolder = outputFolder {
					if Path.isAbsolute(outputFolder) {
						target = outputFolder
					} else {
						target = Path.join(packageFolder, outputFolder)
					}

					target = Path.join(target, input.targets[targetIndex].output)
				}

				normalizer.keywords = generator.reserved
				normalizer.collected = files
				normalizer.nativeEnums = generator.nativeEnums
				normalizer.reservedGlobals = generator.reservedGlobals ?? normalizer.reservedGlobals
				normalizer.package = input
				if let randomSeed = input.randomSeed {
					normalizer.id = randomSeed
				}
				if let globalAccessor = generator.globalAccessor {
					normalizer.globalAccessor = globalAccessor
				}
				normalizer.typer = typer

				#if times
					let beginNormalizer = Date.now()
				#end

				let normal = normalizer.normalize()

				#if times
					console.log("[Normalizer finished in \(Date.now() - beginNormalizer) ms]")
					let beginGenerator = Date.now()
				#end

				// Print hints out
				for hint in project.hints {
					console.log(quote(hint.toString()))
				}

				let outs = generator.perform(normal, target)

				#if times
					console.log("[Generator finished in \(Date.now() - beginGenerator) ms]")
				#end

				if noWrite {
					return outs
				}

				// TODO `mkdir -p` before file write
				Fs.writeFileSync(target, outs)
			} else {
				console.log("Generator for `\(generatorName)` is not registered.")
				Process.exit(1)
			}
		} else {
			console.log("Generator is not defined or unknown. Is there `target.generator` field in `hexa.json`?")
			Process.exit(1)
		}

		let delta = Date.now() - begin
		let lps = Math.round(lines / (delta/1000))
		console.log("[Finished in \(delta) ms, \(lines) lines, \(lps) lines per second]")
		Process.stdout.once('drain', fun () { Process.exit(0) })
		Process.stdout.write('')
		return null
	}

	var typecheckProject: Project = new Project()

	fun typecheck(dir: String): CompilerErrors {
		// Spawn project
		try {

		let project = new Project()
		typecheckProject = project
		let defines: Any = {}
		project.defines = defines
		let input = loadPackage(dir + Path.sep + "hexa.json", targetName, project)

		try {
			loadModules(project, input)
		} catch e: Any {
			return e
		}

		let typer = new Typer(project)
		typer.onlyTypeCheck = true
		try {
			typer.fillModules()
		} catch e: Any {
			return e
		}

		} catch e: Any {
			return e
		}

		return new CompilerErrors([])
	}
}
